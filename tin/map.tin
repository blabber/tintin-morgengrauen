#NOP {== Automapper};

#NOP {Diese Variablen werden in tin/config.tin vorbelegt.};
#NOP {#VARIABLE {MAP[autosave][seconds]} {300}};
#NOP {#VARIABLE {MAP[autosave][git]}     {1}};

#VARIABLE {MAP[file]}       {map/mg.map};
#VARIABLE {MAP[dirty]}      {0};
#VARIABLE {MAP[first_room]} {1};
#VARIABLE {MAP[in_map]}     {0};
#VARIABLE {MAP[loaded]}     {0};
#VARIABLE {MAP[mismatch]}   {1};
#VARIABLE {MAP[center][x]}  {0};
#VARIABLE {MAP[center][y]}  {0};
#VARIABLE {MAP[center][z]}  {0};
#VARIABLE {MAP[override_nofollow][active]} {0};
#VARIABLE {MAP[override_nofollow][value]}  {OFF};

#UNPATHDIR {d}  {u}  {32};
#UNPATHDIR {e}  {w}  {2};
#UNPATHDIR {n}  {s}  {1};
#UNPATHDIR {ne} {sw} {3};
#UNPATHDIR {nw} {se} {9};
#UNPATHDIR {s}  {n}  {4};
#UNPATHDIR {se} {nw} {6};
#UNPATHDIR {sw} {ne} {12};
#UNPATHDIR {u}  {d}  {16};
#UNPATHDIR {w}  {e}  {8};

#PATHDIR {u}  {ob} {32};
#PATHDIR {o}  {w}  {2};
#PATHDIR {n}  {s}  {1};
#PATHDIR {no} {sw} {3};
#PATHDIR {nw} {so} {9};
#PATHDIR {s}  {n}  {4};
#PATHDIR {so} {nw} {6};
#PATHDIR {sw} {no} {12};
#PATHDIR {ob} {u}  {16};
#PATHDIR {w}  {o}  {8};

#ALIAS {unten}      {u};
#ALIAS {osten}      {o};
#ALIAS {norden}     {n};
#ALIAS {nordosten}  {no};
#ALIAS {nordwesten} {nw};
#ALIAS {sueden}     {s};
#ALIAS {suedosten}  {so};
#ALIAS {suedwesten} {sw};
#ALIAS {oben}       {ob};
#ALIAS {westen}     {w};

#EVENT {MAP ENTER MAP}
{
	#VARIABLE {MAP[in_map]} {1};
};

#EVENT {MAP EXIT MAP}
{
	#VARIABLE {MAP[in_map]} {0};
};

#EVENT {MAP CREATE ROOM}
{
	#VARIABLE {MAP[dirty]} {1};
};

#EVENT {MAP DELETE ROOM}
{
	#VARIABLE {MAP[dirty]} {1};
};

#EVENT {MAP ENTER ROOM}
{
	#IF {!$MAP[first_room]}
	{
		#VARIABLE {GMCP[room][info][short]} {};
		#VARIABLE {GMCP[room][info][id]} {};

		@PUBSUB_PUBLISH{map_room_changed};
	};
	#VARIABLE {MAP[first_room]} {0};

	#LOCAL {_roomname} {};
	#LOCAL {_roomarea} {};

	#MAP get {roomname} {_roomname};
	#MAP get {roomarea} {_roomarea};

	#LOCAL {_output} {<190>## Map:<090> };

	#if {"$_roomarea" != ""}
	{
		#FORMAT {_output} {%s[%s] } {$_output} {$_roomarea};
	};

	#FORMAT {_output} {%s<190>%s<090>} {$_output} {$_roomname};

	#SHOW {};
	#SHOW {$_output};
};

#EVENT {MAP EXIT ROOM}
{
	#LOCAL {_old_vnum} {%0};
	#LOCAL {_data}     {};

	#MAP get roomdata {_data} {$_old_vnum};
	#IF {&_data[exits]}
	{
		#LOCAL {_param[vnum]}  {$_old_vnum};
		#LOCAL {_param[exits]} {$_data[exits]};

		#LOCAL {_void} {@_map_check_exits_vnum{$_param}};
	};
};

#IF {$MAP[autosave][seconds] > 0}
{
	#TICKER {map_autosave}
	{
		#IF {$MAP[dirty]}
		{
			@_map_save{Karte aktualisiert (periodisch)};
			#SHOW {<020>autosaved current map...};
			#VARIABLE {MAP[dirty]} {0};
		};
	} {$MAP[autosave][seconds]};
};

#FUNCTION {_map_setup}
{
	#IF {!$MAP[loaded]}
	{
		#MAP read {$MAP[file]};
		#MAP flag vtmap on;
		#MAP return;

		#VARIABLE {MAP[loaded]} {1};
	};

	#MAP offset
		{$SCREEN[map][ul][y]} {$SCREEN[map][ul][x]}
		{$SCREEN[map][lr][y]} {$SCREEN[map][lr][x]};

	#RETURN {#NOP};
};

#FUNCTION {_map_save}
{
	#MAP write {$MAP[file]};

	#IF {$MAP[autosave][git] && "%1" != ""}
	{
		#SYSTEM {git add "$MAP[file]" > /dev/null};
		#SYSTEM {git commit -m "%1" > /dev/null};
	};

	#RETURN {#NOP};
};

#FUNCTION {_map_is_exit_mapped}
{
	#LOCAL {_vnum} {%1};
	#LOCAL {_exit} {%2};

	#IF {!$MAP[in_map]}
	{
		#RETURN {0};
	};


	#LOCAL {_exits} {};
	#MAP get roomexits {_exits} {$_vnum};

	#LOCAL {_target} {@_map_dir_long_short{$_exit}};

	#LOCAL {_exit_name} {};
	#FOREACH {*_exits[%*]} {_exit_name}
	{
		#LOCAL {_exit} {};
		#MAP exit {$_exit_name} save {_exit};

		#IF {"$_exit[name]" == "$_target" || "$_exit[command]" == "$_target"}
		{
			#RETURN {1};
		};
	};

	#RETURN {0};
};

#FUNCTION {_map_check_room_id}
{
	#VARIABLE {MAP[mismatch]} {1};

	#IF {!$MAP[in_map]}
	{
		#RETURN {#NOP};
	};

	#LOCAL {_gmcp_id} {$GMCP[room][info][id]};
	#IF {"$_gmcp_id" == ""}
	{
		#RETURN {#NOP};
	};


	#LOCAL {_map_id} {};
	#MAP get {roomid} {_map_id};

	#IF {"$_map_id" == "$_gmcp_id"}
	{
		#VARIABLE {MAP[mismatch]} {0};
		#RETURN {#NOP};
	};

	#LOCAL {_duplicates} {};
	#MAP list {roomid} {$_gmcp_id} {variable} {_duplicates};
	#IF {&_duplicates[]}
	{
		#ECHO {%cDiese ID (%s) ist bereits kartografiert.} {yellow} {$_gmcp_id};

		#LOCAL {_vnum} {};
		#FOREACH {*_duplicates[%*]} {_vnum}
		{
			#ECHO {%cvnum: %+5s} {yellow} {$_vnum};
		};

		#RETURN {#NOP};
	};


	#IF {"$_map_id" == ""}
	{
		#MAP set {roomid} {$_gmcp_id};
		#VARIABLE {MAP[dirty]} {1};

		#VARIABLE {MAP[mismatch]} {0};
		#RETURN {#NOP};
	};

	#ECHO {%c%s} {yellow} {IDs stimmen nicht ueberein!};
	#ECHO {%cKarte: %s} {yellow} {$_map_id};
	#ECHO {%cGMCP:  %s} {yellow} {$_gmcp_id};

	#RETURN {#NOP};
};

#FUNCTION {_map_check_exits}
{
	#LOCAL {_param[exits]} {%0};

	#IF {!$MAP[in_map]}
	{
		#RETURN {$_param[exits]};
	};

	#MAP get roomvnum {_param[vnum]};

	#RETURN {@_map_check_exits_vnum{$_param}};
}

#FUNCTION {_map_check_exits_vnum}
{
	#LOCAL {_param} {%0};
	#LOCAL {_vnum}  {$_param[vnum]};
	#LOCAL {_exits} {$_param[exits]};

	#IF {!$MAP[in_map] || $MAP[mismatch]}
	{
		#RETURN {$_exits};
	};

	#LOCAL {_room_done} {1};
	#LOCAL {_exit}      {};

	#FOREACH {*_exits[%*]} {_exit}
	{
		#LOCAL {_is_mapped}     {@_map_is_exit_mapped{$_vnum; $_exit}};
		#LOCAL {_exits[$_exit]} {$_is_mapped};

		#IF {!$_is_mapped}
		{
			#LOCAL {_room_done} {0};
		};
	};

	#MAP set roomdata {{exits} {$_exits}} {$_vnum};

	#IF {$_room_done}
	{
		#MAP set roomcolor {} {$_vnum};
	};
	#ELSE
	{
		#MAP set roomcolor {<160>} {$_vnum};
	};

	#RETURN {$_exits};
};

#FUNCTION {_map_on_prompt_received}
{
	#LOCAL {_override_nofollow} {1};
	#IF {"%1" == "regular"}
	{
		#LOCAL {_override_nofollow} {0};
	};

	#IF {$_override_nofollow && $MAP[override_nofollow][active]}
	{
		#RETURN {#NOP};
	};

	#IF {!$_override_nofollow && !$MAP[override_nofollow][active]}
	{
		#RETURN {#NOP};
	};

	#IF {!$MAP[in_map]}
	{
		#RETURN {#NOP};
	};

	#IF {!$_override_nofollow}
	{
		#VARIABLE {MAP[override_nofollow][active]} {0};
		#MAP flag {NoFollow} {$MAP[override_nofollow][value]};

		#RETURN {#NOP};
	};

	#LOCAL {_lines} {};
	#LINE quiet #LINE capture {_lines} #MAP flag;

	#LOCAL {_line} {};
	#FOREACH {$_lines} {_line}
	{
		#REGEXP {$_line} {#MAP: NoFollow flag is set to %w.}
		{
			#VARIABLE {MAP[override_nofollow][active]} {1};
			#VARIABLE {MAP[override_nofollow][value]} {&1};
			#MAP flag {NoFollow} {ON};
		};
	};

	#RETURN {#NOP};
};

#FUNCTION {_map_on_session_disconnected}
{
	@_map_save{Karte aktualisiert (Sitzungsende)};
};

#FUNCTION {_map_exits}
{
	#IF {!$GMCP[room][exits_pending]}
	{
		#RETURN {#NOP};
	};

	#VARIABLE {GMCP[room][exits_pending]} {0};

	#LOCAL {_exit_list}     {$GMCP[room][info][exits]};
	#LOCAL {_checked_exits} {};
	#LOCAL {_exit}          {};

	#FOREACH {$_exit_list[%*]} {_exit}
	{
		#LOCAL {_checked_exits[$_exit]} {0};
	};

	#LOCAL {_checked_exits} {@_map_check_exits{$_checked_exits}};
	@_map_print_exits{sichtbare; {$_checked_exits}};

	#IF {!$MAP[in_map] || $MAP[mismatch]}
	{
		#RETURN {#NOP};
	};

	#LOCAL {_exclusively_mapped_exits} {};
	#LOCAL {_mapped_exits}             {};
	#MAP get roomexits {_mapped_exits};

	#LOCAL {_mapped_exit} {};
	#FOREACH {*_mapped_exits[%*]} {_mapped_exit}
	{
		#LOCAL {_mapped_exit_name} {@_map_dir_short_long{$_mapped_exit}};
		#IF {"$_checked_exits[$_mapped_exit_name]" == ""}
		{
			#LOCAL {_exclusively_mapped_exits[$_mapped_exit_name]} {1};
		};
	};

	@_map_print_exits{manuell erfasste; {$_exclusively_mapped_exits}};

	#RETURN {#NOP};
};

#FUNCTION {_map_print_exits}
{
	#LOCAL {_adjective}  {%1};
	#LOCAL {_exit_list}  {%2};
	#LOCAL {_exit_count} {&_exit_list[]};

	#IF {$_exit_count == 0 || $_exit_count == 1 && "*_exit_list[+1]" == ""}
	{
		#LOCAL  {_output} {};
		#FORMAT {_output} {%cEs gibt keine %sn Ausgaenge.}
			{yellow} {$_adjective};

		#LINE ignore #SHOW {$_output};
		#RETURN {#NOP};
	};

	#IF {$_exit_count == 1}
	{
		#LOCAL {_color} {yellow bold};
		#IF {$_exit_list[+1]}
		{
			#LOCAL {_color} {yellow};
		};

		#LOCAL  {_output} {};
		#FORMAT {_output} {%cEs gibt einen %sn Ausgang: %c\e[4m%s\e[24m%c.}
			{yellow} {$_adjective} {$_color} {*_exit_list[+1]} {yellow};

		#LINE ignore #SHOW {$_output};
		#RETURN {#NOP};
	};

	#SWITCH {$_exit_count}
	{
		#CASE {2} { #LOCAL {_exit_count_string} {zwei} };
		#CASE {3} { #LOCAL {_exit_count_string} {drei} };
		#CASE {4} { #LOCAL {_exit_count_string} {vier} };
		#CASE {5} { #LOCAL {_exit_count_string} {fuenf} };
		#CASE {6} { #LOCAL {_exit_count_string} {sechs} };
		#CASE {7} { #LOCAL {_exit_count_string} {sieben} };
		#CASE {8} { #LOCAL {_exit_count_string} {acht} };
		#CASE {9} { #LOCAL {_exit_count_string} {neun} };
		#DEFAULT  { #LOCAL {_exit_count_string} {$_exit_count} };
	};

	#LOCAL  {_output} {};
	#FORMAT {_output} {%cEs gibt %s %s Ausgaenge: }
		{yellow} {$_exit_count_string} {$_adjective};

	#LOCAL {_i} {0};
	#LOOP {1} {$_exit_count} {_i}
	{
		#SWITCH {$_i}
		{
			#CASE {1}            { #NOP };
			#CASE {$_exit_count} { #FORMAT {_output} {%s und } {$_output} };
			#DEFAULT             { #FORMAT {_output} {%s, }    {$_output} };
		};

		#LOCAL {_color} {yellow bold};
		#IF {$_exit_list[+$_i]}
		{
			#LOCAL {_color} {yellow};
		};

		#FORMAT {_output} {%s%c\e[4m%s\e[24m%c}
			{$_output} {$_color} {*_exit_list[+$_i]} {yellow};
	};

	#CAT {_output} {.};

	#LINE ignore #SHOW {$_output};
	#RETURN {#NOP};
};

#FUNCTION {_map_offset}
{
	#LOCAL {_x} {%1};
	#LOCAL {_y} {%2};
	#LOCAL {_z} {%3};

	#IF {$_x == 0 && $_y == 0 && $_z == 0}
	{

		#VARIABLE {MAP[center][x]} {0};
		#VARIABLE {MAP[center][y]} {0};
		#VARIABLE {MAP[center][z]} {0};

		#MAP center {0} {0} {0};

		#RETURN {#NOP};
	};

	#MATH {MAP[center][x]} {$MAP[center][x] + $_x};
	#MATH {MAP[center][y]} {$MAP[center][y] + $_y};
	#MATH {MAP[center][z]} {$MAP[center][z] + $_z};

	#MAP center {$MAP[center][x]} {$MAP[center][y]} {$MAP[center][z]};

	#RETURN {#NOP};
};

#EVENT {PRESSED LINK MOUSE BUTTON ONE}
{
	%4;
};

#EVENT {MAP ROOM SHORT-CLICKED MOUSE BUTTON ONE}
{
	#LOCAL {_vnum} {%4};

	#LOCAL {_current_vnum} {};
	#MAP get roomvnum {_current_vnum};

	#LOCAL {_void} {0};
	#MAP at {$_vnum} #MAP roomflag void get {_void};

	#IF {$_void}
	{
		#RETURN;
	};

	#IF {$_current_vnum == $_vnum}
	{
		#RETURN;
	};

	#MAP run {$_vnum} {0.25};
};

#FUNCTION {_map_dir_long_short}
{
	#LOCAL {_long} {%1};
	#SWITCH {"$_long"}
	{
		#CASE "unten"      { #RETURN {u} };
		#CASE "osten"      { #RETURN {o} };
		#CASE "norden"     { #RETURN {n} };
		#CASE "nordosten"  { #RETURN {no} };
		#CASE "nordwesten" { #RETURN {nw} };
		#CASE "sueden"     { #RETURN {s} };
		#CASE "suedosten"  { #RETURN {so} };
		#CASE "suedwesten" { #RETURN {sw} };
		#CASE "oben"       { #RETURN {ob} };
		#CASE "westen"     { #RETURN {w} };
		#DEFAULT           { #RETURN {$_long} };
	};
}

#FUNCTION {_map_dir_short_long}
{
	#LOCAL {_short} {%1};
	#SWITCH {"$_short"}
	{
		#CASE "u"  { #RETURN {unten} };
		#CASE "o"  { #RETURN {osten} };
		#CASE "n"  { #RETURN {norden} };
		#CASE "no" { #RETURN {nordosten} };
		#CASE "nw" { #RETURN {nordwesten} };
		#CASE "s"  { #RETURN {sueden} };
		#CASE "so" { #RETURN {suedosten} };
		#CASE "sw" { #RETURN {suedwesten} };
		#CASE "ob" { #RETURN {oben} };
		#CASE "w"  { #RETURN {westen} };
		#DEFAULT   { #RETURN {$_short} };
	};
}

@PUBSUB_SUBSCRIBE{screen_changed;       _map_setup};
@PUBSUB_SUBSCRIBE{prompt_received;      _map_on_prompt_received};
@PUBSUB_SUBSCRIBE{session_disconnected; _map_on_session_disconnected};
@PUBSUB_SUBSCRIBE{gmcp_room_received;   _map_check_room_id};
@PUBSUB_SUBSCRIBE{prompt_received;      _map_exits};
@PUBSUB_SUBSCRIBE{macro_mapoffset;      _map_offset};
