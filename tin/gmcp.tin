#NOP {== GMCP Handling};

#VARIABLE {GMCP[room][exits_pending]} {0};

#EVENT {IAC WILL GMCP}
{
	#LOCAL {_telnet[IAC]}  {\xFF};
	#LOCAL {_telnet[DONT]} {\xFE};
	#LOCAL {_telnet[DO]}   {\xFD};
	#LOCAL {_telnet[WONT]} {\xFC};
	#LOCAL {_telnet[WILL]} {\xFB};
	#LOCAL {_telnet[SB]}   {\xFA};
	#LOCAL {_telnet[SE]}   {\xF0};
	#LOCAL {_telnet[GMCP]} {\xC9};

	#INFO {SYSTEM} {SAVE};

	#SEND {$_telnet[IAC]$_telnet[DO]$_telnet[GMCP]\};
	#SEND {$_telnet[IAC]$_telnet[SB]$_telnet[GMCP]Core.Hello { "client": "$info[SYSTEM][CLIENT_NAME]", "version": "$info[SYSTEM][CLIENT_VERSION]" }$_telnet[IAC]$_telnet[SE]\};
	#SEND {$_telnet[IAC]$_telnet[SB]$_telnet[GMCP]Core.Supports.Set [ "MG.char 1", "MG.room 1", "comm.channel 1" ]$_telnet[IAC]$_telnet[SE]\};
};

#EVENT {IAC SB GMCP MG.char.vitals IAC SE}
{
	#VARIABLE {GMCP[char][vitals]} {%0};
	@_gmcp_prompt{};
};

#EVENT {IAC SB GMCP MG.char.maxvitals IAC SE}
{
	#VARIABLE {GMCP[char][maxvitals]} {%0};
	@_gmcp_prompt{};
};

#EVENT {IAC SB GMCP MG.char.wimpy IAC SE}
{
	#VARIABLE {GMCP[char][wimpy]} {%0};
	@_gmcp_prompt{};
};

#EVENT {IAC SB GMCP MG.room.info IAC SE}
{
	#VARIABLE {GMCP[room][info]} {%0};
	#VARIABLE {GMCP[room][exits_pending]} {1};

	@_gmcp_prompt{};
	@PUBSUB_PUBLISH{gmcp_room_received};
};

#EVENT {IAC SB GMCP comm.channel IAC SE}
{
	#LOCAL {_channel} {%0};
	#LOCAL {_msg}     {$_channel[msg]};
	#LOCAL {_player}  {$_channel[player]};
	#LOCAL {_chan}    {$_channel[chan]};

	#LOCAL {_color} {cyan};
	#IF {"$_chan" == "Tod"}
	{
		#LOCAL {_color} {cyan bold};
	};

	#LOCAL  {_prefix} {};
	#FORMAT {_prefix} {[%s:%s]} {$_chan} {$_player};

	#LOCAL {_emote} {0};
	#REGEXP {$_msg} {$_prefix} {#LOCAL {_emote} {0}} {#LOCAL {_emote} {1}};

	#IF {$_emote}
	{
		#FORMAT {_prefix} {[%s:%s} {$_chan} {$_player};
	};

	#REPLACE {_msg} {$_prefix} {};

	#REPLACE {_msg} {;} {°°°};
	#REPLACE {_msg} {\\n} {;};

	#LOCAL {_line}      {};
	#LOCAL {_firstline} {1};
	#FOREACH {$_msg} {_line}
	{
		#REPLACE {_line} {°°°} {;};

		#LOCAL {_stripped} {};
		#FORMAT {_stripped} {%p} {$_line};
		#IF {"$_stripped" == ""}
		{
			#CONTINUE;
		};

		#IF {$_emote}
		{
			#IF {$_firstline}
			{
				#LOCAL {_firstline} {0};
				#FORMAT {_line} {%c%s %s}
					{$_color} {$_prefix} {$_line};
			};
			#ELSE
			{
				#FORMAT {_line} {%c%s}
					{$_color} {$_line};
			};

		};
		#ELSE
		{
			#FORMAT {_line} {%c%s%c %s}
				{$_color} {$_prefix} {reset} {$_line};
		};

		#SHOW {$_line};

		@PUBSUB_PUBLISH{gmcp_comms_received; {{$_line}}};
	};
};

#FUNCTION {_gmcp_prompt}
{
	#LOCAL {_hp}     {$GMCP[char][vitals][hp]};
	#LOCAL {_sp}     {$GMCP[char][vitals][sp]};
	#LOCAL {_poison} {$GMCP[char][vitals][poison]};
	#LOCAL {_wimpy}  {$GMCP[char][wimpy][wimpy]};

	#LOCAL {_max_hp}     {$GMCP[char][maxvitals][max_hp]};
	#LOCAL {_max_sp}     {$GMCP[char][maxvitals][max_sp]};
	#LOCAL {_max_poison} {$GMCP[char][maxvitals][max_poison]};

	#LOCAL {_room}       {$GMCP[room][info][short]};
	#LOCAL {_domain}     {$GMCP[room][info][domain]};

	#LOCAL {_prompt} {};
	#FORMAT {_prompt}
		{<090>= %sHP: <190>%+2s<090>/%s | }
		{$_prompt} {$_hp} {$_max_hp};

	#IF {"$_sp" != ""}
	{
		#FORMAT {_prompt}
			{%sSP: <190>%+2s<090>/%s }
			{$_prompt} {$_sp} {$_max_sp};

		#IF {"$_wimpy" != ""}
		{
			#FORMAT {_prompt}
				{%s(%s) }
				{$_prompt} {$_wimpy};
		};

		#CAT {_prompt} {| };
	};

	#if {"$_poison" != ""}
	{
		#FORMAT _prompt
			{%sPOI: <190>%+2s<090>/%s | }
			{$_prompt} {$_poison} {$_max_poison};
	};


	#FORMAT {_prompt}
		{%s<090>%s > <190>%s<090>}
		{$_prompt} {$_domain} {$_room};

	#FORMAT {_prompt} {%.$SCREEN[cols]s} {$_prompt};

	#LINE ignore #SHOW {$_prompt} {-2};

	#RETURN {#NOP};
};

@PUBSUB_SUBSCRIBE{screen_changed;   _gmcp_prompt};
@PUBSUB_SUBSCRIBE{map_room_changed; _gmcp_prompt};
